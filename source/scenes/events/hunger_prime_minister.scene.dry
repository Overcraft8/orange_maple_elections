title: The Hunger Prime Minister
subtitle: Popular discontent with Prime Minister [+ prime_minister +] rises.
new-page: true
view-if: year >= 1931 and month >= 2 and unemployed >= 18 and welfare < 0 and works_program == 0
on-arrival: workers_flp *= 0.9 if prime_minister_party == "FLP"; workers_flp *= 0.9 if flp_in_government == 1; workers_scp += 5 if flp_in_government; workers_cp += 5 if flp_in_government; unemployed_scp += 5 if flp_in_government
tags: event
max-visits: 1
face-image: img/Kollwitrpc_Hospital.jpg

= The Hunger Prime Minister

People are calling Prime Minister [+ prime_minister +] the Hunger Prime Minister because of his perceived callousness in the face of the depression. [? if prime_minister_party == "FLP" or flp_in_government : This is obviously a very bad sign for us. ?]

[? if flp_toleration == 1 : As we are currently tolerating the [+ prime_minister +] government, this could very well reflect badly on us. ?]

- @maintain_toleration: Maintain the current governing arrangement.
- @break_toleration: Break toleration; vote against this government.

@maintain_toleration
subtitle: It is necessary to preserve democracy.
view-if: flp_toleration == 1
on-arrival: workers_scp += 5; workers_cp += 5; unemployed_scp += 5; workers_flp -= 5; pro_republic -= 5; cp_relation -= 5

Our policy of toleration continues.


@break_toleration
subtitle: This is likely to result in new elections.
view-if: flp_toleration == 1
on-arrival: next_election_time = time+3 if next_election_time >= time+3; next_parl_election_month = month + 3 if next_election_time >= time+3; next_parl_election_year = year if next_election_time >= time+3; next_parl_election_year += 1 if next_parl_election_month > 12; next_parl_election_month -= 12 if next_parl_election_month > 12; flp_in_government = 0; in_grand_coalition = 0; flp_toleration = 0;

We have definitively broken with the [+ prime_minister +] government, and have voted against them in a vote of no confidence.

New elections have been set for [+ next_parl_election_month : month +] [+ next_parl_election_year +].
